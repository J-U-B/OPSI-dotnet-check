;=====================================================================
; product specific variables  DOTNET
; J. Boettge <boettge@mpi-halle.mpg.de>  2017-07-18 16:07:58 +0200
;=====================================================================

include_insert "%ScriptPath%\variables.opsiinc"

Set $MAP$ 				= getProductMap
Set $FullProductId$     = getValue("id", $MAP$)
Set $ProductName$   	= getValue("name", $MAP$)
Set $Version$       	= getValue("ProductVersion", $MAP$)
Set $SystemType$    	= GetSystemType
Set $NTVersion$   		= GetMsVersionInfo

;-----------------------------------------------------------
; basic requirements
;-----------------------------------------------------------
requiredWinstVersion >= "4.11.6"

if not (CompareDotSeparatedNumbers($NTVersion$, ">=", "6.1"))
	LogError "OS is not Windows 7 or above"
	isFatalError
endif


if not ($SystemType$ = "64 Bit System")
	LogError "OS is not a 64 Bit System"
	isFatalError
endif

;-----------------------------------------------------------
;*** get LogLevel from property
Set $LogLevelDefault$ = GetProductProperty("log_level","default")

;-----------------------------------------------------------
;*** detect testing package:
if (strPos($FullProductId$, "0_") = "0") and (strPos($FullProductId$, "test_") = "0")
	; no a testing package
	set $ProductId$  		= $FullProductId$ 
	set $Testing$ 			= "false"
	if ($LogLevelDefault$ = "default")
		set $LogLevelDefault$  	= "5"
	endif
else
	; this is a testing package
	comment "Prefix for testing package detected"
	set $ProductId$  		= strPart($FullProductId$, calculate(strPos($FullProductId$,"_") + "+1"), strLength($FullProductId$))
	set $Testing$ 			= "true"
	if ($LogLevelDefault$ = "default")
		set $LogLevelDefault$  	= "7"
	endif
endif
setlogLevel 				= $LogLevelDefault$

;-----------------------------------------------------------
;*** detect DFN package:
if strPos($FullProductId$, "dfn_") = "0"
	set $isDFN$ = "false"
	set $StrippedProductId$ = $ProductId$
else
	set $isDFN$ = "true"
	set $StrippedProductId$ = strPart($ProductId$,"5",strLength($ProductId$))
endif

;-----------------------------------------------------------
; software architecture; valid values: "32 bit", "64 bit", "sysnative"

Set $sw_arch$ = GetProductProperty("install_architecture","sysnative")
Set $sw_arch$ = takeString(0, splitString($sw_arch$, " "))

if ( $sw_arch$ = "sysnative" )
	if ($SystemType$ = "64 Bit System")
		Set $sw_arch$ = "64"
	else
		Set $sw_arch$ = "32"
	endif
endif

if ($sw_arch$ = "64")
	; 64 bit software
	set $InstallBase$ = "%ProgramFiles64Dir%"
else
	; 32 bit software
	set $InstallBase$ = EnvVar("ProgramFiles(x86)")
endif

;-----------------------------------------------------------
; language property: "de, "en_US", "en_GB"
Set $sw_lang$ = GetProductProperty("default_language","en_US")

;-----------------------------------------------------------
; post install/uninstall properties:
set $CustomPostInstall$ 	= getProductProperty("custom_post_install","none")
set $CustomPostUninstall$ 	= getProductProperty("custom_post_uninstall","none")

;-----------------------------------------------------------
; is an error a fatal error?
Set $MakeErrorFatal$  		= GetProductProperty("missing_is_fatal","False")

;-----------------------------------------------------------
Set $RegPathUninstall$	 = "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall"
Set $RegPathUninstall64$ = "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall"
Set $RegPathUninstall32$ = "HKEY_LOCAL_MACHINE\SOFTWARE\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall"

;-----------------------------------------------------------
Set $ProductDir$	= stringReplace($ProductName$," ","")
;Set $ProductDir$	= $ProductId$
Set $SrcDir$		= "%ScriptPath%\files\" 
Set $InstallDir$    = $InstallBase$+"\"+$ProductDir$
Set $LogDir$ 	    = "%SystemDrive%\tmp"
Set $InstallLog$    = $LogDir$ + "\"+$ProductId$+"_install.log"
Set $UninstallLog$  = $LogDir$ + "\"+$ProductId$+"_uninstall.log"
Set $TempDir$		= "%opsiTmpDir%"

set $Img_Install$	= "%ScriptPath%\images\" + $StrippedProductId$ +"_install.png"
set $Img_Prepare$	= "%ScriptPath%\images\" + $StrippedProductId$ +"_prepare.png"
set $Img_OK$		= "%ScriptPath%\images\" + $StrippedProductId$ +"_ok.png"
set $Img_FAIL$		= "%ScriptPath%\images\" + $StrippedProductId$ +"_fail.png"

ShowBitmap $Img_Prepare$ $ProductName$

;-----------------------------------------------------------
Set $MinimumSpace$ 	= "1 MB"
Set $MainBin$      	= $StrippedProductId$ +".exe"

;-----------------------------------------------------------
if ( $sw_arch$ = "32" )
	Set $Installer$		  = $SrcDir$ + $ProductName$ + "." + $Version$ + ".exe"
else
	; $sw_arch$ = "64" 
	Set $Installer$		  = $SrcDir$ + $ProductName$ + "." + $Version$ + ".x64.exe"	
	Set $MinimumSpace$ 	= "1 MB"
endif
